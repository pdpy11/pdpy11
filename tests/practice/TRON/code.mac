 
; 3 channels 3-bit player for BK 0011M beeper
; Compile this source with PDPy11: https://github.com/pdpy11/
; Manwe/SandS 2021

	.LINK 40000

	.extern all	; music.mac needs frequency values

		Speaker			= 177716
		MHz				= 4000000.
		LoopDuration	= 352.		; CPU cycles
		LoopCount		= 60.*MHz/LoopDuration/BPM/4


BEGIN:	MOV @#4,Stop+2		; save old STOP vector
		MOV #Stop,@#4		; set new STOP vector
		MOV SP,Stop+10		; save stack pointer
		MOV (SP),Stop+14	; save return address
		MTPS #340			; disable all interruptions
		MOV #100,@#177660	; block keyboard interruption


Play:	CLR R1
		CLR R2
		CLR R3
		MOV #SONG,R5		; pointer to song data

Row:	MOV (R5)+,(PC)+
Note3:	.WORD 0				; note 1 speed

			CLR R4
			ADD @#Note1,R1
			ROL R4
			ADD @#Note2,R2
			ROL R4
			ADD @#Note3,R3
			ROL R4
			MOVB Table(R4),R4
			XOR R4,@#Mix
			MOVB @#Mix,@#Speaker

		MOV (R5)+,(PC)+
Note2:	.WORD 0				; note 2 speed

			CLR R4
			ADD @#Note1,R1
			ROL R4
			ADD @#Note2,R2
			ROL R4
			ADD @#Note3,R3
			ROL R4
			MOVB Table(R4),R4
			XOR R4,@#Mix
			MOVB @#Mix,@#Speaker

		MOV (R5)+,(PC)+
Note1:	.WORD 0				; note 3 speed

			CLR R4
			ADD @#Note1,R1
			ROL R4
			ADD @#Note2,R2
			ROL R4
			ADD @#Note3,R3
			ROL R4
			MOVB Table(R4),R4
			XOR R4,@#Mix
			MOVB @#Mix,@#Speaker

		MOV #LoopCount-4,R0	; 28 cc
1:
			CLR R4
			ADD @#Note1,R1
			ROL R4
			ADD @#Note2,R2
			ROL R4
			ADD @#Note3,R3
			ROL R4
			MOVB Table(R4),R4
			XOR R4,@#Mix
			MOVB @#Mix,@#Speaker

		SOB R0,1	; 352. CPU cycles = 11363,636363 Hz

			CLR R4
			ADD @#Note1,R1
			ROL R4
			ADD @#Note2,R2
			ROL R4
			ADD @#Note3,R3
			ROL R4
			MOVB Table(R4),R4
			XOR R4,@#Mix
			MOVB @#Mix,@#Speaker

		CMP R5,#SONGEND		; check end of song - 32 cc
		BLO Row				; 16 cc

		BR Play				; loop song



Stop:	MOV #120000,@#4		; restore STOP vector
		MOV #1000,SP		; restore stack pointer
		MOV #100000,(SP)	; return address
		RET					; return to DOS

Table:	.BYTE 0,4,40,44,100,104,140,144
Mix:	.WORD 0
; speaker 2th bit quiet  (level 8)
; speaker 5th bit medium (level 24)
; speaker 6th bit loud   (level 64)

	.include "music-tron.mac"


; frequency values

Do1000 = 523251. ; Do frequency 523,25113 Hz * 1000
Q = 65536./(MHz/LoopDuration*1000/Do1000)/4

T1  = 1059463. ; 2^(1/12)
T2  = 1122462. ; 2^(2/12)
T3  = 1189207. ; 2^(3/12)
T4  = 1259921. ; 2^(4/12)
T5  = 1334840. ; 2^(5/12)
T6  = 1414214. ; 2^(6/12)
T7  = 1498307. ; 2^(7/12)
T8  = 1587401. ; 2^(8/12)
T9  = 1681793. ; 2^(9/12)
T10 = 1781797. ; 2^(10/12)
T11 = 1887748. ; 2^(11/12)
M   = 1000000. ; million

C1 = Q/4
Cd1= C1*T1/M
D1 = C1*T2/M
Dd1= C1*T3/M
E1 = C1*T4/M
F1 = C1*T5/M
Fd1= C1*T6/M
G1 = C1*T7/M
Gd1= C1*T8/M
A1 = C1*T9/M
Ad1= C1*T10/M
B1 = C1*T11/M

C2 = Q/2
Cd2= C2*T1/M
D2 = C2*T2/M
Dd2= C2*T3/M
E2 = C2*T4/M
F2 = C2*T5/M
Fd2= C2*T6/M
G2 = C2*T7/M
Gd2= C2*T8/M
A2 = C2*T9/M
Ad2= C2*T10/M
B2 = C2*T11/M

C3 = Q
Cd3= C3*T1/M
D3 = C3*T2/M
Dd3= C3*T3/M
E3 = C3*T4/M
F3 = C3*T5/M
Fd3= C3*T6/M
G3 = C3*T7/M
Gd3= C3*T8/M
A3 = C3*T9/M
Ad3= C3*T10/M
B3 = C3*T11/M

C4 = Q*2
Cd4= C4*T1/M
D4 = C4*T2/M
Dd4= C4*T3/M
E4 = C4*T4/M
F4 = C4*T5/M
Fd4= C4*T6/M
G4 = C4*T7/M
Gd4= C4*T8/M
A4 = C4*T9/M
Ad4= C4*T10/M
B4 = C4*T11/M

C5 = Q*4
Cd5= C5*T1/M
D5 = C5*T2/M
Dd5= C5*T3/M
E5 = C5*T4/M
F5 = C5*T5/M
Fd5= C5*T6/M
G5 = C5*T7/M
Gd5= C5*T8/M
A5 = C5*T9/M
Ad5= C5*T10/M
B5 = C5*T11/M

C6 = Q*8.
Cd6= C6*T1/M
D6 = C6*T2/M
Dd6= C6*T3/M
E6 = C6*T4/M
F6 = C6*T5/M
Fd6= C6*T6/M
G6 = C6*T7/M
Gd6= C6*T8/M
A6 = C6*T9/M
Ad6= C6*T10/M
B6 = C6*T11/M

C7 = Q*16.

	.END
